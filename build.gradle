apply plugin: 'java'
apply plugin: 'maven-publish'

ext {
	sdkRoot = System.getenv("ANDROID_HOME")
	compilerJar = "$rootDir/_libs/compiler.jar"
}
allprojects {
	ext {
		buildDir0 = layout.buildDirectory.getAsFile().get()
	}
	tasks.withType(JavaCompile) {
		options.incremental = true
		options.encoding = 'UTF-8'
		sourceCompatibility = 21
		targetCompatibility = 21

		options.compilerArgs += "-AtargetVersion=8"
//		options.compilerArgs += "--add-exports=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED"
		options.compilerArgs.add("--enable-preview")
		//	options.compilerArgs.add("-verbose")
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository' }
		maven { url 'https://jitpack.io' }
		google()
	}
}

sourceSets.main.java.srcDirs = ["src"]

//force arc version
configurations.configureEach {
	resolutionStrategy.eachDependency { details ->
		if (details.requested.group == 'com.github.Anuken.Arc') details.useVersion "$mindustryVersion"
	}
}
configurations {
	desugarLib
}

dependencies {
	compileOnly project(":annotations")
	annotationProcessor project(":compiler")
	compileOnly project(":testAgent")

//	println "AndroidHome: $sdkRoot"
	def platformRoot = new File("$sdkRoot/platforms").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }
	compileOnly files(
			"assets/libs/reflect-core.jar",
			"$platformRoot/android.jar",
//			"trash/procyon-0.6.jar",
			"_libs/magicClass.jar",
			"_libs/Mindustry_android.jar", // this contains android project of mindustry
			"_libs/Android_dalvik-1.0.jar",
	)
	implementation files("_libs/AndroidField.jar")

	/*annotationProcessor ("com.github.EB-wilson.JavaDynamilizer:apt:$JDERVersion")
	implementation("com.github.EB-wilson.JavaDynamilizer:annotations:$JDERVersion")
	implementation("com.github.EB-wilson.JavaDynamilizer:core:$JDERVersion")
	implementation("com.github.EB-wilson.JavaDynamilizer:baseimpl:$JDERVersion")*/
//	annotationProcessor "com.github.EB-wilson.UniverseCore:annotations:$uncVersion"
//	compileOnly "com.github.EB-wilson.UniverseCore:core:$uncVersion"


	compileOnly("com.github.anuken.mindustry:core:$mindustryVersion") {
		exclude module: "flabel"
	}
//	mindustryVersion = "v146"
	compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
	compileOnly "com.github.Anuken.Arc:backend-android:$mindustryVersion"
	compileOnly "com.github.Anuken.Arc:backend-sdl:$mindustryVersion"
//	compileOnly 'com.github.bsideup:jabel:1.0.1'
//	annotationProcessor 'com.github.bsideup:jabel:1.0.1'
//	compileOnly("com.github.Anuken.rhino::$mindustryVersion")
	desugarLib 'com.android.tools:desugar_jdk_libs:2.1.5'
}
task jarAndroid(type: JavaExec) {
    dependsOn "jar"
    group = "build"
    description = "Compiles jar to dex using D8 (JavaExec mode)"

    // --------------------------------------------------------
    // 1. 配置增量构建 (缓存关键)
    // --------------------------------------------------------
    def inputJar = layout.buildDirectory.file("libs/${project.archivesBaseName}-${version}.jar")
    def outputJar = layout.buildDirectory.file("libs/${project.archivesBaseName}-android.jar")

    // 输入：Jar包
    inputs.file(inputJar).withPropertyName("inputJar")

    // 输入：编译依赖 (修正：改名为 d8Dependencies 防止冲突)
    inputs.files(configurations.compileClasspath).withPropertyName("d8Dependencies")

    // 输入：API版本
    inputs.property("minApi", minApi)

    // 输出：生成的文件
    outputs.file(outputJar).withPropertyName("outputJar")

    // 允许任务结果缓存
    outputs.cacheIf { true }

    // --------------------------------------------------------
    // 2. 配置执行环境
    // --------------------------------------------------------
    mainClass = "com.android.tools.r8.D8"
	jvmArgs "-XX:+UseZGC -XX:+ZGenerational"

    // --------------------------------------------------------
    // 3. 动态计算参数 (放在 doFirst)
    // --------------------------------------------------------
    doFirst {
        if (!sdkRoot || !new File(sdkRoot).exists()) {
            throw new GradleException("ANDROID_HOME not set or invalid.")
        }

        // 查找 Android Jar
        def platformRoot = new File("$sdkRoot/platforms/").listFiles()?.sort()?.reverse()?.find {
            new File(it, "android.jar").exists()
        }
        if (!platformRoot) throw new GradleException("No android.jar found.")
        def platformJar = new File(platformRoot, "android.jar")

        // 查找 D8
        def d8Root = file("$sdkRoot/build-tools/").listFiles()?.sort()?.reverse()?.find { dir ->
            dir.isDirectory() &&
            new File(dir, "lib/d8.jar").exists() &&
            (dir.name.tokenize('.')[0].isInteger() ? dir.name.tokenize('.')[0].toInteger() >= (minApi as int) : true)
        }
        if (!d8Root) throw new GradleException("No suitable d8.jar found in build-tools.")

        // 设置运行 D8 本身的 Classpath
        classpath = files(new File(d8Root, "lib/d8.jar"))

        println "D8 Exec: Using ${d8Root.name}"

        // 准备依赖路径
        def dependencies = configurations.compileClasspath.collectMany { ["--classpath", it.path] }

        // 设置运行参数
        args = [
                "--lib", platformJar.path,
                "--min-api", minApi,
//                "--release",
                "--output", outputJar.get().asFile.path,
                inputJar.get().asFile.path
        ] + dependencies
    }
}

jar {
	archiveFileName = "${project.archivesBaseName}-${version}.jar"

	from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) })

	from(rootDir) { include "mod.hjson" }
	from("assets/") { include "**" }
	from(zipTree("${project(":annotations").buildDir0}/libs/annotations-${version}.jar")) {
		include "**/settings/**"
	}

	evaluationDependsOn(":testAgent") // 确保 testAgent 的 build.gradle 先运行
	def agent = project(":testAgent")
	from("${agent.buildDir0}/libs/") { include "testAgent-${agent.version}.jar" rename { o -> "libs/hotswap-agent.jar" } }
}


task playDesktop(type: Copy) {
	dependsOn jar

	println "buildDir: $buildDir0; version: $version"

	from "$buildDir0/libs"
	into "${MdtDataDir}/mods"
	include "$archivesBaseName-${version}.jar"
	rename { oldName -> "$modName-${version}.jar" }
}

task playGame(type: JavaExec) {
	dependsOn playDesktop

	//运行游戏目录
	classpath(MdtDataDir + "/Mindustry.jar")
//	jvmArgs("-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005")
	jvmArgs("-XX:+AllowEnhancedClassRedefinition")
//	jvmArgs = [
//			"-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"
//	]
	args = [
//			'-testMobile'
//			'-debug'
	]
}

task playMultiplayer(type: JavaExec) {
	dependsOn jar

	doFirst {
		copy {
			from "$buildDir0/libs"
			into "${MdtDataDir}/mods"
			include "$archivesBaseName-${version}.jar"
			rename { oldName -> "$modName-${version}.jar" }
		}
		new Thread(() -> {
			Thread.sleep(5000)
			exec {
				commandLine "java", "-jar", MdtDataDir + "/Mindustry.jar"
			}
		}).start()
	}


	classpath(MdtDataDir + "/Mindustry.jar")

	args = [
			'-debug',
	]
}

task ZCompileAll(type: Jar) {
	dependsOn jarAndroid
	archiveFileName = "$buildDir0/libs/$modName[$version].jar"

	from(zipTree("$buildDir0/libs/$modName-${version}.jar")) { include "**" }
	from(zipTree("$buildDir0/libs/$modName-android.jar")) { include "classes.dex" }
	doLast {
		copy {
			from "$buildDir0/libs/$modName[$version].jar"
			into "$buildDir0/libs/"
			rename("\\[$version]", "-deploy")
		}
	}
}

tasks.register('mergeLibs', Jar) {
	def libName = "reflect-core"
	archiveFileName = "$rootDir/assets/libs/${libName}.jar"

	def libProject = "$rootDir/../localLib/$libName"
	def libsDir = new File("$libProject/build/libs")

	from(zipTree("$libsDir/$libName-desktop.jar")) {
		exclude "classes.dex"
//		exclude "mod.hjson"
		exclude "META-INF/META-INF.MF"
	}
	from(zipTree("$libsDir/$libName-dx.jar")) {
		include "**"
	}
}

task playAndroid {
	dependsOn ZCompileAll

	def adb = "F:/files/java/sdk/platform-tools/adb"
	def pkg =
			//"com.github.tinylake.mindustryX"
			"io.anuke.mindustry"
	def MDT_ANDROID_DIR =
			"/storage/emulated/0/Android/data/$pkg/files"

	def buildDir = buildDir0
	def fileName = "$archivesBaseName[$version].jar"
	doLast {
		("$adb push $buildDir/libs/$fileName $MDT_ANDROID_DIR/mods/$fileName")
				.execute().waitForProcessOutput(System.out, System.err)

		Thread.sleep(100)
		"$adb shell am start -n $pkg/mindustry.android.AndroidLauncher -W -S"
				.execute().waitForProcessOutput(System.out, System.err)
		Thread.sleep(100)
//		"$adb "

		if (false) {
			"$adb push $project.rootDir/appendLog.sh $MDT_ANDROID_DIR/appendLog.sh"
					.execute().waitForProcessOutput(System.out, System.err)
			"$adb shell sh ${MDT_ANDROID_DIR}/appendLog.sh"
					.execute().waitForProcessOutput(System.out, System.err)
		}
	}
}