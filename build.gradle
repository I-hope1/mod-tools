apply plugin: 'java'

ext {
	sdkRoot = System.getenv("ANDROID_HOME")
	compilerJar = "$rootDir/_libs/compiler.jar"
}
allprojects {
	ext {
		buildDir0 = layout.buildDirectory.getAsFile().get()
	}
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		sourceCompatibility = 21
		targetCompatibility = 21

		options.compilerArgs += "-AtargetVersion=8"
		options.compilerArgs.add("--add-exports=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED")
	}

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository' }
		maven { url 'https://www.jitpack.io' }
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs.add("--enable-preview")
//	options.compilerArgs.add("-verbose")
}

sourceSets.main.java.srcDirs = ["src"]

//force arc version
configurations.configureEach {
	resolutionStrategy.eachDependency { details ->
		if (details.requested.group == 'com.github.Anuken.Arc') details.useVersion "$mindustryVersion"
	}
}

dependencies {
	compileOnly project(":annotations")
	annotationProcessor project(":compiler")

//	def sdkRoot = System.getenv("ANDROID_HOME")
//	println "AndroidHome: $sdkRoot"
//	def platformRoot = new File("$sdkRoot/platforms").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }
//	if (new File("${MdtDataDir}/Mindustry_desktop.jar").exists())
//		compileOnly files("${MdtDataDir}/Mindustry_desktop.jar")
	compileOnly files(
			"_libs/Mindustry_android.jar",
			"assets/libs/reflect-core.jar",
//			"trash/procyon-0.6.jar",
			"_libs/Android_dalvik-1.0.jar",
			"_libs/AndroidField.jar"
	)
	compileOnly("com.github.Anuken.Mindustry:core:$mindustryVersion") {
		exclude module: "flabel"
	}
	compileOnly("com.github.Anuken.Arc:arc-core:$mindustryVersion")
}

task jarAndroid(type: JavaExec) {
	dependsOn "jar"

	if (!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set to your Android SDK directory.");

	def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }

	if (!platformRoot) throw new GradleException("No android.jar found. Ensure that you have an Android platform installed.")

	//collect dependencies needed for desugaring
	def dependencies = (configurations.compileClasspath.asList()
//			+ configurations.runtimeClasspath.asList()
//			+ [new File(platformRoot, "android.jar")]
	).collectMany { ["--classpath", "$it.path"] }

	def d8Root = file("$sdkRoot/build-tools/").listFiles().find {
		it.listFiles().any { it.name.contains("d8") } && Integer.valueOf(it.name.substring(0, 2)) >= Integer.parseInt(minApi)
	}

	classpath("$d8Root/lib/d8.jar")
	main("com.android.tools.r8.D8")
	//dex and desugar files - this requires d8 in your PATH
	args = dependencies + [
			"--min-api", "$minApi",
//			"--file-per-class",
			"--output", "$buildDir0/libs/${project.archivesBaseName}-android.jar",
			"$buildDir0/libs/${project.archivesBaseName}-${version}.jar",
	]
}

jar {
	archiveFileName = "${project.archivesBaseName}-${version}.jar"

	from(rootDir) { include "mod.hjson" }
	from("assets/") { include "**" }
	from(zipTree("_libs/AndroidField.jar").getAsFileTree()) { include "**" }
}

task playGame(type: JavaExec) {
	dependsOn jar

	println "buildDir: $buildDir0; version: $version"

	doFirst {
		copy {
			from "$buildDir0/libs"
			into "${MdtDataDir}/mods"
			include "$archivesBaseName-${version}.jar"
			rename { oldName -> "$modName-${version}.jar" }
		}
	}
	//运行游戏目录
	classpath(MdtDataDir + "/Mindustry.jar")
//	jvmArgs = [
//			'-D' + 'socksProxyHost=127.0.0.1',
//			'-D' + 'socksProxyPort=1080']
	args = [
//	        '-testMobile'
//			'-debug'
	]
}

task ZCompileAll(type: Jar) {
	dependsOn jarAndroid
	archiveFileName = "$buildDir0/libs/$modName[$version].jar"

	from(zipTree("$buildDir0/libs/$modName-${version}.jar")) { include "**" }
	from(zipTree("$buildDir0/libs/$modName-android.jar")) { include "classes.dex" }
	doLast {
		copy {
			from "$buildDir0/libs/$modName[$version].jar"
			into "$buildDir0/libs/"
			rename("\\[$version]", "-deploy")
		}
	}
}

tasks.register('mergeLibs', Jar) {
	def libName = "reflect-core"
	archiveFileName = "$rootDir/assets/libs/${libName}.jar"

	def libProject = "$rootDir/../localLib/$libName"
	def libsDir = new File("$libProject/build/libs")

	from(zipTree("$libsDir/$libName-desktop.jar")) {
		exclude "classes.dex"
//		exclude "mod.hjson"
		exclude "META-INF/META-INF.MF"
	}
	from(zipTree("$libsDir/$libName-dx.jar")) {
		include "**"
	}
}

task playAndroid {
	dependsOn ZCompileAll

	def adb = "F:/files/java/sdk/platform-tools/adb"
	def MDT_ANDROID_DIR = "/storage/emulated/0/Android/data/io.anuke.mindustry/files"

	def buildDir = buildDir0
	def fileName = "$archivesBaseName[$version].jar"
	doLast {
		("$adb push $buildDir/libs/$fileName $MDT_ANDROID_DIR/mods/")
				.execute().waitForProcessOutput(System.out, System.err)

		"$adb push $project.rootDir/appendLog.sh $MDT_ANDROID_DIR/appendLog.sh"
				.execute().waitForProcessOutput(System.out, System.err)
		"$adb shell am start -n io.anuke.mindustry/mindustry.android.AndroidLauncher -W -S"
				.execute().waitForProcessOutput(System.out, System.err)
		Thread.sleep(100)
		"$adb shell sh ${MDT_ANDROID_DIR}/appendLog.sh"
				.execute().waitForProcessOutput(System.out, System.err)
	}
}