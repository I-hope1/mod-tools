apply plugin: 'java'
group 'mod-tools'
// 这是文件版本
version '1.4.5'

static def export(CompileOptions options) {
// options.compilerArgs.addAll("-Xlint:deprecation")
	options.compilerArgs.addAll([
			"java.base/jdk.internal.misc",
			"java.base/jdk.internal.loader",
			"java.base/jdk.internal.reflect",
			// ---------------
			"jdk.compiler/com.sun.tools.javac.api",
			"jdk.compiler/com.sun.tools.javac.comp",
			"jdk.compiler/com.sun.tools.javac.code",
			"jdk.compiler/com.sun.tools.javac.tree",
			"jdk.compiler/com.sun.tools.javac.main",
			"jdk.compiler/com.sun.tools.javac.model",
			"jdk.compiler/com.sun.tools.javac.jvm",
			"jdk.compiler/com.sun.tools.javac.parser",
			"jdk.compiler/com.sun.tools.javac.processing",
			"jdk.compiler/com.sun.tools.javac.util",
			"jdk.compiler/com.sun.tools.javac.resources",
			"java.base/sun.reflect.annotation",
			"java.base/jdk.internal.module",
			"jdk.jfr/jdk.jfr.internal"
	].collectMany { ["--add-exports", "$it=ALL-UNNAMED"] })
//	println options.compilerArgs.join(" ")
}

allprojects {
	ext {
		uncVersion = '1.8.9'
		mindustryVersion = 'v146'

		MdtDataDir = 'E:/Users/ASUS/Desktop/Mindustry136'
		buildDir0 = layout.buildDirectory.getAsFile().get()
		sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
	}
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		sourceCompatibility = 9
		targetCompatibility = 9
		export(options)
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
		maven { url 'https://www.jitpack.io' }
	}
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 17
	targetCompatibility = 17
}

sourceSets.main.java.srcDirs = ["src"]

dependencies {
	compileOnly project(":annotations")
	annotationProcessor project(":compiler")

//	def sdkRoot = System.getenv("ANDROID_HOME")
//	println "AndroidHome: $sdkRoot"
//	def platformRoot = new File("$sdkRoot/platforms").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }
	if (new File("${MdtDataDir}/Mindustry_desktop.jar").exists())
		compileOnly files("${MdtDataDir}/Mindustry_desktop.jar")
	compileOnly files(
			"_libs/Mindustry_android.jar",
//			"$platformRoot/android.jar",
			"assets/libs/reflect-core.jar",
			"trash/procyon-0.6.jar",
			"_libs/Android_dalvik-1.0.jar",
	)
	implementation files(
			"_libs/AndroidField.jar",
	)
	compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
	compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
}

// --------------------------
//force arc version
configurations.configureEach {
	resolutionStrategy.eachDependency { details ->
		if (details.requested.group == 'com.github.Anuken.Arc') {
			details.useVersion "$mindustryVersion"
		}
	}
}
task jarAndroid {
	dependsOn "jar"

	doLast {
		if (!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set to your Android SDK directory.");

		def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }

		if (!platformRoot) throw new GradleException("No android.jar found. Ensure that you have an Android platform installed.")

		//collect dependencies needed for desugaring
		def dependencies = (configurations.compileClasspath.asList()).collect { "--classpath $it.path" }.join(" ")

		def d8 = new File("$sdkRoot/build-tools/").listFiles().sort().reverse()
				.collectMany { f -> [new File(f, "d8"), new File(f, "d8.bat")] }
				.find {it.exists() }

		//dex and desugar files - this requires d8 in your PATH
		"$d8 $dependencies --min-api 30 --output ${project.archivesBaseName}Android.jar ${project.archivesBaseName}Desktop.jar"
				.execute(null, new File("$buildDir0/libs"))
				.waitForProcessOutput(System.out, System.err)
	}
}
jar {
	archiveFileName = "${archivesBaseName}Desktop.jar"

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	from(rootDir) { include "mod.hjson" }
	from("assets/") { include "**" }
}
task deploy(type: Jar) {
	dependsOn jar
	dependsOn jarAndroid
	archiveFileName = "${archivesBaseName}.jar"

	from { [zipTree("$buildDir0/libs/${project.archivesBaseName}Desktop.jar"), zipTree("$buildDir/libs/${project.archivesBaseName}Android.jar")] }
}
// ---------------------------------

task playGame(type: JavaExec) {
	dependsOn classes
	dependsOn jar

	println "buildDir: $buildDir0; version: $version"
	doFirst {
		copy {
			from "$buildDir0/libs"
			into "${MdtDataDir}/mods"
			include "$archivesBaseName-${version}.jar"
			rename { oldName -> "$project.group-${version}.jar" }
		}
	}
	//运行游戏目录
	classpath(MdtDataDir + "/Mindustry.jar")
	args = [
//	        '-testMobile'
	]
	jvmArgs = [
			'-XX:+UseParallelGC',
			'-Djava.library.path=E:/Users/ASUS/Desktop/Mods/mod-tools136/assets',
			'-DsocksProxyHost=127.0.0.1',
			'-DsocksProxyPort=1080',
			'-DsocksNonProxyHosts="localhost|127.0.0.1"'
	]
}
task ZCompileAll {
	dependsOn deploy

	doLast {
		def libs = file("$buildDir0/libs/")
		file(libs, "${rootProject.name}-${version}.jar")
				.renameTo(file(libs, "${rootProject.name.replace("136", "")}-$version"))
	}
}

tasks.register('mergeLibs', Jar) {
	def libName = "reflect-core"
	archiveFileName = "$rootDir/assets/libs/${libName}.jar"

	def libProject = "$rootDir/../localLib/$libName"
	def libsDir = new File("$libProject/build/libs")

	from(zipTree("$libsDir/$libName-desktop.jar")) {
		exclude "classes.dex"
//		exclude "mod.hjson"
		exclude "META-INF/META-INF.MF"
	}
	from(zipTree("$libsDir/$libName-dx.jar")) {
		include "**"
	}
}

task playAndroid {
	dependsOn deploy

	def adb = "F:/files/java/sdk/platform-tools/adb"
	def MDT_ANDROID_DIR = "/storage/emulated/0/Android/data/io.anuke.mindustry/files"

	def buildDir = buildDir0
	def fileName = "$archivesBaseName-${version}.jar"
	doLast {
		("$adb push $buildDir/tmp/deploy/$fileName $MDT_ANDROID_DIR/mods/")
				.execute().waitForProcessOutput(System.out, System.err)

		"$adb push $project.rootDir/appendLog.sh $MDT_ANDROID_DIR/appendLog.sh"
				.execute().waitForProcessOutput(System.out, System.err)
		"$adb shell am start -n io.anuke.mindustry/mindustry.android.AndroidLauncher -W -S"
				.execute().waitForProcessOutput(System.out, System.err)
		Thread.sleep(100)
		"$adb shell sh ${MDT_ANDROID_DIR}/appendLog.sh"
				.execute().waitForProcessOutput(System.out, System.err)
	}
}
